buildscript {
    ext.kotlin_version = '1.3.10'
    ext.ktor_version = '1.0.0-rc'
    ext.dokka_version = '0.9.17'

    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://dl.bintray.com/kotlin/kotlin-eap" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-serialization:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:$dokka_version"
    }
}

allprojects {
    group 'jp.nephy'

    apply plugin: 'kotlin'
    apply plugin: 'kotlinx-serialization'
    apply plugin: 'org.jetbrains.dokka'

    repositories {
        mavenCentral()
        jcenter()
        maven { url "https://dl.bintray.com/kotlin/kotlin-eap" }
        maven { url "https://dl.bintray.com/kotlin/ktor" }
        maven { url "https://dl.bintray.com/kotlin/kotlinx" }
    }

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
        kotlinOptions.freeCompilerArgs += ["-Xuse-experimental=kotlin.Experimental"]
    }

    compileTestKotlin {
        kotlinOptions.jvmTarget = "1.8"
        kotlinOptions.freeCompilerArgs += ["-Xuse-experimental=kotlin.Experimental"]
    }

    dokka {
        outputFormat = 'html'
        outputDirectory = "$buildDir/javadoc"
        includeNonPublic = false
        skipDeprecated = false
        reportUndocumented = true
        skipEmptyPackages = true
        impliedPlatforms = ["JVM"]
    }
}

subprojects {
    dependencies {
        compile rootProject
    }

    task fatJar(type: Jar) {
        manifest {
            attributes "Main-Class": "jp.nephy.glados.MainKt"
        }
        baseName = "glados-full"
        from {
            configurations.compile.findAll {
                !it.name.endsWith('pom')
            }.collect {
                it.isDirectory() ? it : zipTree(it)
            }
        }
        with jar
    }
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"

    /* Discord */
    compile "net.dv8tion:JDA:3.8.1_443"
    compile "com.jagrosh:jda-utilities-commons:2.1.4"

    /* MusicBot */
    compile "com.sedmelluq:lavaplayer:1.3.10"
    compile "com.sedmelluq:jda-nas:1.0.6"
    compile "com.google.apis:google-api-services-youtube:v3-rev206-1.25.0"
    compile "com.rometools:rome:1.11.1"

    /* Twitter API */
    compile "jp.nephy:penicillin:3.2.0"
    compile "jp.nephy:jsonkt:4.0"
    compile "org.jetbrains.kotlinx:kotlinx-serialization-runtime:0.9.0"

    /* Database */
    compile "org.litote.kmongo:kmongo:3.9.0"

    /* Web Server & Client */
    compile "io.ktor:ktor-server-netty:$ktor_version"
    compile "io.ktor:ktor-html-builder:$ktor_version"
    compile "io.ktor:ktor-client-apache:$ktor_version"

    /* Hot reloading */
    compile "io.methvin:directory-watcher:0.8.1"

    /* Misc */
    compile "org.apache.commons:commons-lang3:3.8.1"

    /* Logging */
    compile "io.github.microutils:kotlin-logging:1.6.20"
    compile "ch.qos.logback:logback-core:1.2.3"
    compile "ch.qos.logback:logback-classic:1.2.3"
    compile "org.fusesource.jansi:jansi:1.17.1"
    compile "org.openjdk.jol:jol-core:0.9"
}
